plugins {
    id 'fabric-loom' version '1.6-SNAPSHOT'
    id 'maven-publish'
    id "com.github.johnrengelman.shadow" version "7.0.0"

}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    maven {
        name = 'Ladysnake Mods'
        url = 'https://maven.ladysnake.org/releases'
        content {
            includeGroup 'io.github.ladysnake'
            includeGroup 'org.ladysnake'
            includeGroupByRegex 'dev\\.onyxstudios.*'
        }
    }
}

configurations {
    embed {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom(embed)
    runtimeClasspath.extendsFrom(embed)
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    modImplementation "org.ladysnake:satin:${satin_version}"
    include "org.ladysnake:satin:${satin_version}"


    /* ImGui dependencies */
    implementation "io.github.spair:imgui-java-binding:${project.imguiVersion}"
    shadow "io.github.spair:imgui-java-binding:${project.imguiVersion}"
    implementation "io.github.spair:imgui-java-lwjgl3:${project.imguiVersion}"
    shadow "io.github.spair:imgui-java-lwjgl3:${project.imguiVersion}"

    implementation "io.github.spair:imgui-java-natives-windows:${project.imguiVersion}"
    shadow "io.github.spair:imgui-java-natives-windows:${project.imguiVersion}"
    implementation "io.github.spair:imgui-java-natives-linux:${project.imguiVersion}"
    shadow "io.github.spair:imgui-java-natives-linux:${project.imguiVersion}"
    implementation "io.github.spair:imgui-java-natives-macos:${project.imguiVersion}"
    shadow "io.github.spair:imgui-java-natives-macos:${project.imguiVersion}"

    embed fileTree(dir: "lib", include: ['*.jar'])
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}"}
    }
    from {
        configurations.embed.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "loader_version", project.loader_version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "minecraft_version": project.minecraft_version,
                "loader_version": project.loader_version
    }
}

def targetJavaVersion = 17
tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release.set(targetJavaVersion)
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}"}
    }
}

// configure the maven publication
publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.archives_base_name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {

    }
}
